#!/usr/bin/env perl

#
# Install the precompiled blastplus.
#
# Build from source on mac crashes with a segfault.
# Build from source on ubuntu also fails.
#

use strict;

use Carp;
use File::Basename;
use Cwd 'abs_path';

my $dest = $ENV{TARGET} || "/kb/runtime";
my $parallel = 4;

if (@ARGV)
{
    $dest = shift;
    print STDERR "Overriding destination to $dest\n";
}

my $u = `sw_vers 2> /dev/null`;
my $which;
if ($? == 0 && $u =~ /Mac OS/)
{
    $which = "universal-macosx";
}
elsif (`uname` =~ /linux/i)
{
    $which = "x64-linux";
}
else
{
    die "Cannot determine platform";
}

my $vers = "2.2.28";
my $tar = "ncbi-blast-${vers}+-${which}.tar.gz";
my $dir = "ncbi-blast-${vers}+";
my $url = "ftp://ftp.ncbi.nlm.nih.gov/blast/executables/blast+/$vers/$tar";

if (! -s $tar)
{
    run("curl", "-o", $tar, "-L", $url);
}

run("tar", "-x", "-z", "-f", $tar);
opendir(B, "$dir/bin") or die "Cannot opendir $dir/bin: $!";
for my $f (sort grep { $_ !~ /^\./ && -x "$dir/bin/$_" } readdir(B))
{
    my $dest_file = "$dest/bin/$f";
    if (-f "$dest_file")
    {
	$dest_file = "$dest/bin/${f}+";
    }
    run("cp", "$dir/bin/$f", $dest_file);
}

sub run
{
    my(@cmd) = @_;
    print "@cmd\n";
    my $rc = system(@cmd);
    if ($rc != 0)
    {
	die "Command failed with rc=$rc: @cmd\n";
    }
}
