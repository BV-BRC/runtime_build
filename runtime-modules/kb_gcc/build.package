#!/usr/bin/env perl

#
# We build a whole modern GCC toolchain here.
#

use strict;
use File::Basename;
use File::Find;
use File::Path qw(make_path);
use Cwd qw(abs_path getcwd);
my $cwd = getcwd();

use Getopt::Long::Descriptive;

my($opt, $usage) = describe_options("%c %o [target]",
				    ["subdir=s", "Install into this subdirectory of the target"],
				    ["help|h", "Show this usage message"]);
print($usage->text), exit if $opt->help;
die($usage->text) if @ARGV > 1;

my $top_dest = $ENV{TARGET} || "/kb/runtime";
if (@ARGV)
{
    $top_dest = shift;
}

my $dest = $top_dest;
$dest .= "/" . $opt->subdir if $opt->subdir;

print "using $dest as installation directory\n";
system ("mkdir", "-p", $dest) unless -e $dest;

my $dir;
$ENV{PATH} = "$dest/bin:$ENV{PATH}";
## module load ##
if(!system("bash", "-c", "module avail > /dev/null 2>&1")) {
  my $cmd = "./gcc_module.sh";
  my $modules = `$cmd`;
  my @gccs;
  foreach my $line (split /[\n\s]+/, $modules) {
    $line =~ s/\(default\)//;
    push @gccs, $1 if $line =~ /(gcc\S+)/i;
  }

  my @vers;
  foreach my $gcc (@gccs) {
    my $ver = find_ver($gcc);
    push @vers, $ver;
    print $gcc, "is at ver ", $ver, "\n";
  }

  my $latest = find_latest(@vers);
  my @module = grep /$latest/, @gccs;
  my $cmd = "./gcc_load.sh \"$module[0]\"";
  my $paths = `$cmd` or die;
  my @paths = split /[\s\n]+/, $paths;
  foreach (@paths) {
    my $base = basename($_);
    run("mkdir", "-p", "$dest/bin");
    unlink "$dest/bin/$base" if -e "$dest/bin/$base";
    run("ln",  "-s",  "$_", "$dest/bin/$base");
  }
} ## end module load ##

else {
  $dir = download_tool("http://ftp.gnu.org/gnu/binutils/binutils-2.26.tar.gz", $dest);
  build_tool($dir, $dest);

  do_chdir($cwd);
  $dir = download_tool("http://mirror.team-cymru.org/gnu/gcc/gcc-4.9.3/gcc-4.9.3.tar.gz", $dest);
  mkdir("gcc-build") or die "mkdir gcc-build failed: $!";
  do_chdir("gcc-build");
  run("../$dir/configure", "--prefix", $dest, "--disable-multilib");
  run("make", "-j", 6);
  run("make", "install");

  do_chdir($cwd);
  $dir = download_tool("http://mirror.team-cymru.org/gnu/gdb/gdb-7.11.tar.gz");
  build_tool($dir, $dest);

}

do_chdir($cwd);
$dir = download_tool("http://downloads.sourceforge.net/project/boost/boost/1.60.0/boost_1_60_0.tar.bz2");
$dir = abs_path($dir);
do_chdir($dir);
do_chdir("tools/build");
run("./bootstrap.sh");
run("./b2", "install", "--prefix=$dest");
do_chdir($dir);
run("b2", "-j", 6, "toolset=gcc", "stage");
run("b2", "toolset=gcc", "--prefix=$dest", "install");

sub download_tool
{
    my($url, $dest) = @_;

    my $file = basename($url);
    my($dir,$comp) = $file =~ /^(.*)\.(tar\.gz|tgz|tar\.bz2)$/;

    my %comp_option = ('tar.gz' => 'z',
		       'tgz' => 'z',
		       'tar.bz2' => 'j');
    my $comp_option = $comp_option{$comp};

    if (! -s $file)
    {
	run("curl", "-o", $file, "-L", $url);
    }

    system("rm", "-rf", $dir);
    run("tar",  "xf$comp_option",  $file);

    -d $dir or die "$dir not found after extraction";
    return $dir;
}

sub build_tool
{
    my($dir, $dest) = @_;

    chdir $dir or die "could not chdir $dir: $!";

    run("./configure", "--prefix", $dest);
	
    run("make", "-j", "6");
    run("make", "install");
}
    
sub do_chdir
{
    my($dir) = @_;
    if (!chdir($dir))
    {
	my $here = getcwd();
	die "chdir $dir from $here failed: $!";
    }
}

sub run
{
    my(@cmd) =@_;
    print "@cmd\n";
    my $rc = system(@cmd);
    if ($rc != 0)
    {
	die "Command failed with rc=$rc: @cmd\npath=$ENV{PATH}\n";
    }
}


# subs for module load
sub find_ver {
  my $r = $1 if $_[0] =~ /([\d\.]+)/;
  warn "could not find_ver" unless $r;
  return $r;
}

sub find_latest {
  my $latest = 0; # latest is numerical representation of ver
  my $highest;    # highest is the highest version
  foreach my $ver (@_) {
    my $num = $ver;
    $num =~ s/\.//g;
    if ($latest < $num) {
      $latest = $num;
      $highest = $ver;
    }
  }
  return $highest;
}
# end subs for module load
